apiVersion: templates.gatekeeper.sh/v1
kind: ConstraintTemplate
metadata:
  name: d8verifyimagesignature
  labels:
    heritage: deckhouse
    module: admission-policy-engine
    security.deckhouse.io: external-data
spec:
  crd:
    spec:
      names:
        kind: D8VerifyImageSignature
  targets:
    - rego: |
        package ratifyverification

        # Get data from Ratify
        remote_data := response {
          container_images := [img | img = input.review.object.spec.containers[_].image]
          init_container_images := [img | img = input.review.object.spec.initContainers[_].image]
          ephemeral_container_images := [img | img = input.review.object.spec.ephemeralContainers[_].image]
          images := array.concat(container_images, array.concat(init_container_images, ephemeral_container_images))
          response := external_data({"provider": "ratify-provider", "keys": images})
        }

        # Base Gatekeeper violation
        violation[{"msg": msg}] {
          general_violation[{"result": msg}]
        }

        # Check if there are any system errors
        general_violation[{"result": result}] {
          err := remote_data.system_error
          err != ""
          result := sprintf("System error calling external data provider: %s", [err])
        }

        # Check if there are errors for any of the images
        general_violation[{"result": result}] {
          count(remote_data.errors) > 0
          result := sprintf("Error validating one or more images: %s", remote_data.errors)
        }

        # Check if the success criteria is true
        general_violation[{"result": result}] {
          subject_validation := remote_data.responses[_]
          subject_validation[1].isSuccess == false
          result := sprintf("Subject failed verification: %s", [subject_validation[0]])
        }
      target: admission.k8s.gatekeeper.sh
