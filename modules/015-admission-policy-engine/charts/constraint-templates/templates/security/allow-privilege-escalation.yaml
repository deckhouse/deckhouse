apiVersion: templates.gatekeeper.sh/v1
kind: ConstraintTemplate
metadata:
  name: d8allowprivilegeescalation
  labels:
    heritage: deckhouse
    module: admission-policy-engine
    security.deckhouse.io: security-policy
  annotations:
    metadata.gatekeeper.sh/title: "Allow Privilege Escalation in Container"
    description: >-
      Controls restricting escalation to root privileges. Corresponds to the
      `allowPrivilegeEscalation` field in a PodSecurityPolicy. For more
      information, see https://kubernetes.io/docs/concepts/security/pod-security-standards/
spec:
  crd:
    spec:
      names:
        kind: D8AllowPrivilegeEscalation
      validation:
        openAPIV3Schema:
          type: object
          description: >-
            Controls restricting escalation to root privileges. Corresponds to the
            `allowPrivilegeEscalation` field in a PodSecurityPolicy. For more
            information, see https://kubernetes.io/docs/concepts/security/pod-security-standards/
  targets:
    - target: admission.k8s.gatekeeper.sh
      code:
        - engine: Rego
          source:
            version: "v1"
            rego: |
              package d8.pod_security_policies

              violation contains {"msg": msg, "details": {}} if {
                not input.review.operation == "DELETE"
                c := input_containers[_]
                not allowed_by_any(c)
                allowed_by_policy := "allowPrivilegeEscalation: false"
                actual_value := get_actual_privilege_escalation(c)
                exception_allowed := get_exception_allowed(c)
                msg := build_message(c.name, allowed_by_policy, actual_value, exception_allowed)
              }

              get_actual_privilege_escalation(c) := out if {
                has_field(c, "securityContext")
                has_field(c.securityContext, "allowPrivilegeEscalation")
                out := sprintf("allowPrivilegeEscalation: %v", [c.securityContext.allowPrivilegeEscalation])
              }

              get_actual_privilege_escalation(c) := "allowPrivilegeEscalation: not set (defaults to true)" if {
                not has_field(c, "securityContext")
              }

              get_actual_privilege_escalation(c) := "allowPrivilegeEscalation: not set (defaults to true)" if {
                has_field(c, "securityContext")
                not has_field(c.securityContext, "allowPrivilegeEscalation")
              }

              get_exception_allowed(c) := out if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                has_field(exception.spec.securityContext, "allowPrivilegeEscalation")
                has_field(exception.spec.securityContext.allowPrivilegeEscalation, "allowedValue")
                out := sprintf("SecurityPolicyException allowed: allowPrivilegeEscalation: %v", [exception.spec.securityContext.allowPrivilegeEscalation.allowedValue])
              }

              get_exception_allowed(_) := "" if {
                not input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
              }

              get_exception_allowed(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                not data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
              }

              get_exception_allowed(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                not has_field(exception.spec, "securityContext")
              }

              get_exception_allowed(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                not has_field(exception.spec.securityContext, "allowPrivilegeEscalation")
              }

              get_exception_allowed(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                has_field(exception.spec.securityContext, "allowPrivilegeEscalation")
                not has_field(exception.spec.securityContext.allowPrivilegeEscalation, "allowedValue")
              }

              build_message(name, allowed_by_policy, actual_value, exception_allowed) := msg if {
                exception_allowed != ""
                msg := sprintf("Privilege escalation container is not allowed: %v. | %v | %v | %v", [name, actual_value, allowed_by_policy, exception_allowed])
              }

              build_message(name, allowed_by_policy, actual_value, exception_allowed) := msg if {
                exception_allowed == ""
                msg := sprintf("Privilege escalation container is not allowed: %v. | %v | %v", [name, actual_value, allowed_by_policy])
              }

              allowed_by_any(c) if {
                allowed_by_securitypolicyexception(c)
              }

              allowed_by_any(c) if {
                allowed_by_securitypolicy(c)
              }

              allowed_by_securitypolicyexception(c) if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                has_field(exception.spec.securityContext, "allowPrivilegeEscalation")
                has_field(exception.spec.securityContext.allowPrivilegeEscalation, "allowedValue")
                exception.spec.securityContext.allowPrivilegeEscalation.allowedValue == c.securityContext.allowPrivilegeEscalation
              }

              allowed_by_securitypolicy(c) if {
                has_field(c, "securityContext")
                c.securityContext.allowPrivilegeEscalation == false
              }

              input_containers contains c if {
                c := input.review.object.spec.containers[_]
              }

              input_containers contains c if {
                c := input.review.object.spec.initContainers[_]
              }

              input_containers contains c if {
                c := input.review.object.spec.ephemeralContainers[_]
              }

              # has_field returns whether an object has a field
              has_field(object, field) if {
                object[field]
              }

              has_field(object, field) := false if {
                not object[field]
              }
