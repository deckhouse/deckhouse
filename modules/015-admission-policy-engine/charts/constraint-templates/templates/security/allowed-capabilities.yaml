apiVersion: templates.gatekeeper.sh/v1
kind: ConstraintTemplate
metadata:
  name: d8allowedcapabilities
  labels:
    heritage: deckhouse
    module: admission-policy-engine
    security.deckhouse.io: security-policy
  annotations:
    metadata.gatekeeper.sh/title: "The allowed capabilities"
    metadata.gatekeeper.sh/version: 1.0.0
    description: >-
      Controls Linux capabilities on containers. Corresponds to the
      `allowedCapabilities` in a PodSecurityPolicy. For more information, see
      https://kubernetes.io/docs/concepts/security/pod-security-standards/
spec:
  crd:
    spec:
      names:
        kind: D8AllowedCapabilities
      validation:
        # Schema for the `parameters` field
        openAPIV3Schema:
          type: object
          description: >-
            Controls Linux capabilities on containers. Corresponds to the
            `allowedCapabilities` in a PodSecurityPolicy. For more information, see
            https://kubernetes.io/docs/concepts/security/pod-security-standards/
          properties:
            allowedCapabilities:
              type: array
              description: "A list of Linux capabilities that can be added to a container."
              items:
                type: string
            requiredDropCapabilities:
              type: array
              description: "A list of Linux capabilities that are required to be dropped from a container."
              items:
                type: string
  targets:
    - target: admission.k8s.gatekeeper.sh
      code:
        - engine: Rego
          source:
            version: "v1"
            rego: |
              package d8.security_policies

              violation contains {"msg": msg, "details": {}} if {
                not input.review.operation == "DELETE"
                container := input_containers[_]
                not allowed_by_any(container)
                allowed := get_default(input.parameters, "allowedCapabilities", "NONE")
                allowed_by_policy := sprintf("Allowed capabilities: %v", [allowed])
                actual_value := get_actual_capabilities(container)
                exception_allowed := get_exception_allowed_capabilities(container)
                msg := build_capabilities_message(container.name, allowed_by_policy, actual_value, exception_allowed)
              }

              get_actual_capabilities(container) := out if {
                has_field(container, "securityContext")
                has_field(container.securityContext, "capabilities")
                has_field(container.securityContext.capabilities, "add")
                add_caps := container.securityContext.capabilities.add
                out := sprintf("Actual capabilities.add: %v", [add_caps])
              }

              get_actual_capabilities(container) := "Actual capabilities.add: [] (not set)" if {
                not has_field(container, "securityContext")
              }

              get_actual_capabilities(container) := "Actual capabilities.add: [] (not set)" if {
                has_field(container, "securityContext")
                not has_field(container.securityContext, "capabilities")
              }

              get_actual_capabilities(container) := "Actual capabilities.add: [] (not set)" if {
                has_field(container, "securityContext")
                has_field(container.securityContext, "capabilities")
                not has_field(container.securityContext.capabilities, "add")
              }

              get_exception_allowed_capabilities(container) := out if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                has_field(exception.spec.securityContext, "capabilities")
                has_field(exception.spec.securityContext.capabilities, "allowedValues")
                allowed_add := exception.spec.securityContext.capabilities.allowedValues.add
                allowed_drop := exception.spec.securityContext.capabilities.allowedValues.drop
                out := sprintf("SecurityPolicyException allowed: capabilities.add: %v, capabilities.drop: %v", [allowed_add, allowed_drop])
              }

              get_exception_allowed_capabilities(container) := out if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                has_field(exception.spec.securityContext, "capabilities")
                has_field(exception.spec.securityContext.capabilities, "allowedValues")
                allowed_add := exception.spec.securityContext.capabilities.allowedValues.add
                not exception.spec.securityContext.capabilities.allowedValues.drop
                out := sprintf("SecurityPolicyException allowed: capabilities.add: %v, capabilities.drop: []", [allowed_add])
              }

              get_exception_allowed_capabilities(_) := "" if {
                not input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
              }

              get_exception_allowed_capabilities(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                not data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
              }

              get_exception_allowed_capabilities(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                not has_field(exception.spec, "securityContext")
              }

              get_exception_allowed_capabilities(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                not has_field(exception.spec.securityContext, "capabilities")
              }

              get_exception_allowed_capabilities(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                has_field(exception.spec.securityContext, "capabilities")
                not has_field(exception.spec.securityContext.capabilities, "allowedValues")
              }

              build_capabilities_message(name, allowed_by_policy, actual_value, exception_allowed) := msg if {
                exception_allowed != ""
                msg := sprintf("container <%v> has a disallowed capability. | %v | %v | %v", [name, actual_value, allowed_by_policy, exception_allowed])
              }

              build_capabilities_message(name, allowed_by_policy, actual_value, exception_allowed) := msg if {
                exception_allowed == ""
                msg := sprintf("container <%v> has a disallowed capability. | %v | %v", [name, actual_value, allowed_by_policy])
              }

              has_field(object, field) if {
                object[field]
              }

              has_field(object, field) := false if {
                not object[field]
              }

              allowed_by_any(container) if {
                allowed_by_securitypolicyexception(container)
              }

              allowed_by_any(container) if {
                allowed_by_securitypolicy(container)
              }

              allowed_by_securitypolicyexception(container) if {
                  label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                  namespace := input.review.object.metadata.namespace
                  exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                  has_field(exception.spec, "securityContext")
                  has_field(exception.spec.securityContext, "capabilities")
                  has_field(exception.spec.securityContext.capabilities, "allowedValues")
                  allowed_add := {c | c := lower(exception.spec.securityContext.capabilities.allowedValues.add[_])}
                  allowed_drop := {c | c := lower(exception.spec.securityContext.capabilities.allowedValues.drop[_])}
                  capabilities_add := {c | c := lower(container.securityContext.capabilities.add[_])}
                  capabilities_drop := {c | c := lower(container.securityContext.capabilities.drop[_])}
                  
                  count(capabilities_add - allowed_add) == 0
                  count(capabilities_drop - allowed_drop) == 0
              }

              allowed_by_securitypolicy(container) if {
                allowed := {c | c := lower(input.parameters.allowedCapabilities[_])}
                not allowed.ALL
                has_field(container, "securityContext")
                has_field(container.securityContext, "capabilities")
                has_field(container.securityContext.capabilities, "add")
                capabilities := {c | c := lower(container.securityContext.capabilities.add[_])}
                count(capabilities - allowed) == 0
              }

              allowed_by_securitypolicy(container) if {
                allowed := {c | c := lower(input.parameters.allowedCapabilities[_])}
                not allowed.ALL
                not has_field(container, "securityContext")
              }

              allowed_by_securitypolicy(container) if {
                allowed := {c | c := lower(input.parameters.allowedCapabilities[_])}
                not allowed.ALL
                has_field(container, "securityContext")
                not has_field(container.securityContext, "capabilities")
              }

              allowed_by_securitypolicy(container) if {
                allowed := {c | c := lower(input.parameters.allowedCapabilities[_])}
                not allowed.ALL
                has_field(container, "securityContext")
                has_field(container.securityContext, "capabilities")
                not has_field(container.securityContext.capabilities, "add")
              }


              violation contains {"msg": msg, "details": {}} if {
                  not input.review.operation == "DELETE"
                  container := input_containers[_]
                  not drops_required_capabilities_by_any(container)
                  allowed_by_policy := sprintf("Required drop capabilities: %v or \"ALL\"", [input.parameters.requiredDropCapabilities])
                  actual_value := get_actual_drop_capabilities(container)
                  exception_allowed := get_exception_allowed_drop_capabilities(container)
                  msg := build_drop_capabilities_message(container.name, allowed_by_policy, actual_value, exception_allowed)
              }

              get_actual_drop_capabilities(container) := out if {
                has_field(container, "securityContext")
                has_field(container.securityContext, "capabilities")
                has_field(container.securityContext.capabilities, "drop")
                drop_caps := container.securityContext.capabilities.drop
                out := sprintf("Actual capabilities.drop: %v", [drop_caps])
              }

              get_actual_drop_capabilities(container) := "Actual capabilities.drop: [] (not set)" if {
                not has_field(container, "securityContext")
              }

              get_actual_drop_capabilities(container) := "Actual capabilities.drop: [] (not set)" if {
                has_field(container, "securityContext")
                not has_field(container.securityContext, "capabilities")
              }

              get_actual_drop_capabilities(container) := "Actual capabilities.drop: [] (not set)" if {
                has_field(container, "securityContext")
                has_field(container.securityContext, "capabilities")
                not has_field(container.securityContext.capabilities, "drop")
              }

              get_exception_allowed_drop_capabilities(container) := out if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                has_field(exception.spec.securityContext, "capabilities")
                has_field(exception.spec.securityContext.capabilities, "allowedValues")
                allowed_drop := exception.spec.securityContext.capabilities.allowedValues.drop
                out := sprintf("SecurityPolicyException allowed: capabilities.drop: %v", [allowed_drop])
              }

              get_exception_allowed_drop_capabilities(_) := "" if {
                not input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
              }

              get_exception_allowed_drop_capabilities(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                not data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
              }

              get_exception_allowed_drop_capabilities(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                not has_field(exception.spec, "securityContext")
              }

              get_exception_allowed_drop_capabilities(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                not has_field(exception.spec.securityContext, "capabilities")
              }

              get_exception_allowed_drop_capabilities(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                has_field(exception.spec.securityContext, "capabilities")
                not has_field(exception.spec.securityContext.capabilities, "allowedValues")
              }

              build_drop_capabilities_message(name, allowed_by_policy, actual_value, exception_allowed) := msg if {
                exception_allowed != ""
                msg := sprintf("container <%v> is not dropping all required capabilities. | %v | %v | %v", [name, actual_value, allowed_by_policy, exception_allowed])
              }

              build_drop_capabilities_message(name, allowed_by_policy, actual_value, exception_allowed) := msg if {
                exception_allowed == ""
                msg := sprintf("container <%v> is not dropping all required capabilities. | %v | %v", [name, actual_value, allowed_by_policy])
              }

              drops_required_capabilities_by_any(container) if {
                  drops_required_capabilities_by_securitypolicyexception(container)
              }

              drops_required_capabilities_by_any(container) if {
                  drops_required_capabilities_by_securitypolicy(container)
              }

              drops_required_capabilities_by_securitypolicyexception(container) if {
                  label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                  namespace := input.review.object.metadata.namespace
                  exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                  has_field(exception.spec, "securityContext")
                  has_field(exception.spec.securityContext, "capabilities")
                  has_field(exception.spec.securityContext.capabilities, "allowedValues")
                  must_drop := {c | c := lower(exception.spec.securityContext.capabilities.allowedValues.drop[_])}
                  capabilities_drop := {c | c := lower(container.securityContext.capabilities.drop[_])}

                  count(must_drop - capabilities_drop) == 0
              }


              drops_required_capabilities_by_securitypolicy(container) if {
                  must_drop := {c | c := lower(input.parameters.requiredDropCapabilities[_])}
                  all := {"all"}
                  capabilities_drop := {c | c := lower(container.securityContext.capabilities.drop[_])}

                  (count(must_drop - capabilities_drop) == 0) 
                  (count(all - capabilities_drop) == 0)
              }

              input_containers contains c if {
                c := input.review.object.spec.containers[_]
              }

              input_containers contains c if {
                c := input.review.object.spec.initContainers[_]
              }

              input_containers contains c if {
                c := input.review.object.spec.ephemeralContainers[_]
              }

              get_default(obj, param, _default) := out if {
                out = obj[param]
              }

              get_default(obj, param, _default) := out if {
                not obj[param]
                not obj[param] == false
                out = _default
              }
