apiVersion: templates.gatekeeper.sh/v1
kind: ConstraintTemplate
metadata:
  name: d8privilegedcontainer
  labels:
    heritage: deckhouse
    module: admission-policy-engine
    security.deckhouse.io: security-policy
  annotations:
    metadata.gatekeeper.sh/title: "Privileged Container"
    description: >-
      Controls the ability of any container to enable privileged mode.
      Corresponds to the `privileged` field in a PodSecurityPolicy. For more
      information, see
      https://kubernetes.io/docs/concepts/policy/pod-security-policy/#privileged
spec:
  crd:
    spec:
      names:
        kind: D8PrivilegedContainer
      validation:
        openAPIV3Schema:
          type: object
          description: >-
            Controls the ability of any container to enable privileged mode.
            Corresponds to the `privileged` field in a PodSecurityPolicy. For more
            information, see
            https://kubernetes.io/docs/concepts/security/pod-security-standards/
  targets:
    - target: admission.k8s.gatekeeper.sh
      code:
        - engine: Rego
          source:
            version: "v1"
            rego: |
              package d8.security_policies

              violation contains {"msg": msg, "details": {}} if {
                not input.review.operation == "DELETE"
                c := input_containers[_]
                not allowed_by_any(c)
                allowed_by_policy := "privileged: false"
                actual_value := get_actual_privileged(c)
                exception_allowed := get_exception_allowed(c)
                msg := build_message(c.name, allowed_by_policy, actual_value, exception_allowed)
              }

              get_actual_privileged(c) := out if {
                has_field(c, "securityContext")
                has_field(c.securityContext, "privileged")
                out := sprintf("privileged: %v", [c.securityContext.privileged])
              }

              get_actual_privileged(c) := "privileged: not set (defaults to false)" if {
                not has_field(c, "securityContext")
              }

              get_actual_privileged(c) := "privileged: not set (defaults to false)" if {
                has_field(c, "securityContext")
                not has_field(c.securityContext, "privileged")
              }

              get_exception_allowed(c) := out if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                has_field(exception.spec.securityContext, "privileged")
                has_field(exception.spec.securityContext.privileged, "allowedValue")
                out := sprintf("SecurityPolicyException allowed: privileged: %v", [exception.spec.securityContext.privileged.allowedValue])
              }

              get_exception_allowed(_) := "" if {
                not input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
              }

              get_exception_allowed(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                not data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
              }

              get_exception_allowed(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                not has_field(exception.spec, "securityContext")
              }

              get_exception_allowed(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                not has_field(exception.spec.securityContext, "privileged")
              }

              get_exception_allowed(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                has_field(exception.spec.securityContext, "privileged")
                not has_field(exception.spec.securityContext.privileged, "allowedValue")
              }

              build_message(name, allowed_by_policy, actual_value, exception_allowed) := msg if {
                exception_allowed != ""
                msg := sprintf("Privileged container is not allowed: %v. | %v | %v | %v", [name, actual_value, allowed_by_policy, exception_allowed])
              }

              build_message(name, allowed_by_policy, actual_value, exception_allowed) := msg if {
                exception_allowed == ""
                msg := sprintf("Privileged container is not allowed: %v. | %v | %v", [name, actual_value, allowed_by_policy])
              }

              has_field(object, field) if {
                object[field]
              }

              has_field(object, field) := false if {
                not object[field]
              }

              allowed_by_any(c) if {
                allowed_by_securitypolicyexception(c)
              }

              allowed_by_any(c) if {
                allowed_by_securitypolicy(c)
              }

              allowed_by_securitypolicyexception(c) if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "securityContext")
                has_field(exception.spec.securityContext, "privileged")
                has_field(exception.spec.securityContext.privileged, "allowedValue")
                exception.spec.securityContext.privileged.allowedValue == c.securityContext.privileged
              }

              allowed_by_securitypolicy(c) if {
                not c.securityContext.privileged
              }

              input_containers contains c if {
                c := input.review.object.spec.containers[_]
              }

              input_containers contains c if {
                c := input.review.object.spec.initContainers[_]
              }

              input_containers contains c if {
                c := input.review.object.spec.ephemeralContainers[_]
              }
