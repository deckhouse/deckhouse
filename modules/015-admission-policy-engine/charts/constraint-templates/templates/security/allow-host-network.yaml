apiVersion: templates.gatekeeper.sh/v1
kind: ConstraintTemplate
metadata:
  name: d8hostnetwork
  labels:
    heritage: deckhouse
    module: admission-policy-engine
    security.deckhouse.io: security-policy
  annotations:
    metadata.gatekeeper.sh/title: "Host Networking"
    metadata.gatekeeper.sh/version: 1.0.0
    description: >-
      Controls usage of host network namespace by pod containers. Specific
      ports must be specified. Corresponds to the `hostNetwork` and
      `hostPorts` fields in a PodSecurityPolicy. For more information, see
      https://kubernetes.io/docs/concepts/policy/pod-security-policy/#host-namespaces
spec:
  crd:
    spec:
      names:
        kind: D8HostNetwork
      validation:
        # Schema for the `parameters` field
        openAPIV3Schema:
          type: object
          description: >-
            Controls usage of host network namespace by pod containers. Specific
            ports must be specified. Corresponds to the `hostNetwork` and
            `hostPorts` fields in a PodSecurityPolicy. For more information, see
            https://kubernetes.io/docs/concepts/policy/pod-security-policy/#host-namespaces
          properties:
            allowHostNetwork:
              description: "Determines if the policy allows the use of HostNetwork in the pod spec."
              type: boolean
            ranges:
              type: array
              description: "A list of of hostPort ranges allowed by the rule."
              items:
                type: object
                description: "The range of hostPorts allowed by the rule."
                properties:
                  min:
                    description: "The start of the allowed port range, inclusive."
                    type: integer
                  max:
                    description: "The end of the allowed port range, inclusive."
                    type: integer
  targets:
    - target: admission.k8s.gatekeeper.sh
      code:
        - engine: Rego
          source:
            version: "v1"
            rego: |
              package d8.security_policies

              violation contains {"msg": msg, "details": {}} if {
                not input.review.operation == "DELETE"
                not allowed_by_any(input.review.object)
                object.get(input.review.object.spec, "hostNetwork", false) == false
                allowed_by_policy := sprintf("Allowed values: %v", [input.parameters])
                actual_value := get_actual_hostnetwork_value(input.review.object)
                exception_allowed := get_exception_allowed_hostnetwork(input.review.object)
                msg := build_hostnetwork_message(input.review.object.metadata.name, allowed_by_policy, actual_value, exception_allowed)
              }
              violation contains {"msg": msg, "details": {}} if {
                not input.review.operation == "DELETE"
                not allowed_by_any(input.review.object)
                object.get(input.review.object.spec, "hostNetwork", false) == true
                allowed_by_policy := sprintf("Allowed values: %v", [input.parameters])
                actual_value := get_actual_hostnetwork_value(input.review.object)
                exception_allowed := get_exception_allowed_hostnetwork(input.review.object)
                msg := build_hostnetwork_message(input.review.object.metadata.name, allowed_by_policy, actual_value, exception_allowed)
              }

              get_actual_hostnetwork_value(obj) := out if {
                hostNetwork := object.get(obj.spec, "hostNetwork", false)
                hostPorts := [hp | hp := input_containers[_].ports[_].hostPort]
                containerPorts := [cp | cp := input_containers[_].ports[_].containerPort]
                out := sprintf("Actual hostNetwork: %v, hostPorts: %v, containerPorts: %v", [hostNetwork, hostPorts, containerPorts])
              }

              get_exception_allowed_hostnetwork(obj) := out if {
                label := obj.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := obj.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "network")
                has_field(exception.spec.network, "hostNetwork")
                has_field(exception.spec.network.hostNetwork, "allowedValue")
                has_field(exception.spec.network, "hostPorts")
                allowed_hostnetwork := exception.spec.network.hostNetwork.allowedValue
                allowed_hostports := exception.spec.network.hostPorts
                out := sprintf("SecurityPolicyException allowed: hostNetwork: %v, hostPorts: %v", [allowed_hostnetwork, allowed_hostports])
              }

              get_exception_allowed_hostnetwork(_) := "" if {
                not input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
              }

              get_exception_allowed_hostnetwork(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                not data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
              }

              get_exception_allowed_hostnetwork(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                not has_field(exception.spec, "network")
              }

              get_exception_allowed_hostnetwork(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "network")
                not has_field(exception.spec.network, "hostNetwork")
              }

              get_exception_allowed_hostnetwork(_) := "" if {
                label := input.review.object.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := input.review.object.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "network")
                has_field(exception.spec.network, "hostNetwork")
                not has_field(exception.spec.network.hostNetwork, "allowedValue")
              }

              build_hostnetwork_message(pod_name, allowed_by_policy, actual_value, exception_allowed) := msg if {
                exception_allowed != ""
                msg := sprintf("The hostNetwork or hostPort are not allowed, pod: %v. | %v | %v | %v", [pod_name, actual_value, allowed_by_policy, exception_allowed])
              }

              build_hostnetwork_message(pod_name, allowed_by_policy, actual_value, exception_allowed) := msg if {
                exception_allowed == ""
                msg := sprintf("The hostNetwork or hostPort are not allowed, pod: %v. | %v | %v", [pod_name, actual_value, allowed_by_policy])
              }
              allowed_by_any(obj) if {
                allowed_by_securitypolicyexeption(obj)
              }

              # or
              allowed_by_any(obj) if {
                allowed_by_securitypolicy(obj)
              }

              allowed_by_securitypolicyexeption(obj) if {
                label := obj.metadata.labels["security.deckhouse.io/security-policy-exception"]
                namespace := obj.metadata.namespace
                exception := data.inventory.namespace[namespace]["deckhouse.io/v1alpha1"].SecurityPolicyException[label]
                has_field(exception.spec, "network")
                has_field(exception.spec.network, "hostNetwork")
                has_field(exception.spec.network.hostNetwork, "allowedValue")
                has_field(exception.spec.network, "hostPorts")

                allowed_hostnetwork_by_exception(obj, exception)
                allowed_hostports_by_exception(obj, exception)
              }

              allowed_hostnetwork_by_exception(obj, exception) if {
                obj_hostNetwork := object.get(obj.spec, "hostNetwork", false)
                exception.spec.network.hostNetwork.allowedValue == obj_hostNetwork
              }

              allowed_hostports_by_exception(obj, exception) if {
                obj.spec.hostNetwork == true
                object_host_ports := [{"port": entry.hostPort, "protocol": entry.protocol} | entry := input_containers[_].ports[_]]
                object_container_ports := [{"port": entry.containerPort, "protocol": entry.protocol} | entry := input_containers[_].ports[_]]
                allowed_ports := [{"port": entry.port, "protocol": entry.protocol} | entry := exception.spec.network.hostPorts[_]]
                every port in array.concat(object_host_ports, object_container_ports) {
                  port in allowed_ports
                }
              }

              allowed_hostports_by_exception(obj, exception) if {
                object.get(obj.spec, "hostNetwork", false) == false
                object_host_ports := [{"port": entry.hostPort, "protocol": entry.protocol} | entry := input_containers[_].ports[_]]
                allowed_ports := [{"port": entry.port, "protocol": entry.protocol} | entry := exception.spec.network.hostPorts[_]]
                every port in object_host_ports {
                  port in allowed_ports
                }
              }

              allowed_by_securitypolicy(obj) if {
                allowed_hostnetwork_by_securitypolicy(obj)
                allowed_hostports_by_securitypolicy(obj)
              }

              allowed_hostnetwork_by_securitypolicy(_) if {
                input.parameters.allowHostNetwork == true
              }

              allowed_hostnetwork_by_securitypolicy(obj) if {
                object.get(obj.spec, "hostNetwork", false) == false
              }
 

              allowed_hostports_by_securitypolicy(obj) if {
                object.get(obj.spec, "hostNetwork", false) == false
                hostPorts := [hp | hp := input_containers[_].ports[_].hostPort ]
                in_range(input.parameters.ranges, hostPorts)
              }


              allowed_hostports_by_securitypolicy(obj) if {
                obj.spec.hostNetwork == true
                hostPorts := [hp | hp := input_containers[_].ports[_].hostPort ]
                containerPorts := [cp | cp := input_containers[_].ports[_].containerPort]
                ports = array.concat(hostPorts, containerPorts)
                in_range(input.parameters.ranges, ports)
              }
            
              in_range(ranges, ports) if {
                every port in ports{
                  matching := {1 | port >= ranges[j].min; port <= ranges[j].max}
                  count(matching) > 0
                }
              }

              input_containers contains c if {
                c := input.review.object.spec.containers[_]
              }

              # or
              input_containers contains c if {
                c := input.review.object.spec.initContainers[_]
              }

              # or
              input_containers contains c if {
                c := input.review.object.spec.ephemeralContainers[_]
              }

              # has_field returns whether an object has a field
              has_field(object, field) if {
                object[field]
              }

              has_field(object, field) := false if {
                not object[field]
              }
